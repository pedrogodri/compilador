#Options
GenerateScanner = true
GenerateParser = true
Language = Java
ScannerName = Lexico
ParserName = Sintatico
SemanticName = Semantico
ScannerCaseSensitive = true
ScannerTable = Compact
Input = String
Parser = LL
#RegularDefinitions
mai: [A-Z]
min : [a-z]
dig: [0-9]
dez_milhar: [1-9] {dig}? {dig}? {dig}?  {dig}?
dez_milhar_invert: {dig}? {dig}? {dig}? {dig}? [1-9]

bloco: "{" [^\{\}]* "}"
linha: "\\" (.)* "\\"

#Tokens
identificador: ({min} | {mai} {min})+ (_{dig}+)?
cint: 0 | {dez_milhar}
cfloat: (0 | {dez_milhar})"."({dez_milhar_invert} | 0)
cstring: \"[^\n\""\"]*\"

pr_add = identificador: "add"
pr_and = identificador: "and"
pr_begin = identificador: "begin"
pr_bool = identificador: "bool"
pr_count = identificador: "count"
pr_delete = identificador: "delete"
pr_do = identificador: "do"
pr_elementof = identificador: "elementOf"
pr_else = identificador: "else"
pr_end = identificador: "end"
pr_false = identificador: "false"
pr_float = identificador: "float"
pr_if = identificador: "if"
pr_int = identificador: "int"
pr_list = identificador: "list"
pr_not = identificador: "not"
pr_or = identificador: "or"
pr_print = identificador: "print"
pr_read = identificador: "read"
pr_size = identificador: "size"
pr_string = identificador: "string"
pr_true = identificador: "true"
pr_until = identificador: "until"


"+" 
"-" 
"*" 
"/" 
"==" 
"~=" 
"<" 
">" 
"=" 
"<-" 
"(" 
")" 
";"
","


:[\s\t\n]

:!{bloco}
:!{linha}
#NonTerminals
<programa>
<lista_instrucoes>
<instrucao>
<comando>


<dec_variavel>
<comando_entrada_dados>
<comando_saida_dados>
<comando_selecao>
<comando_repeticao>

<tipo>
<lista_id>
<tipo_simples>
<lista_tipo>

<expressao>
<elemento>
<posicao>

<lista_entrada>
<string_read>

<lista_expressoes>

<lista_comandos>
<else>

<expressao_>
<valor>
<relacional>
<relacional_>
<operador_relacional>
<aritmetica>
<aritmetica_>
<termo>
<termo_>
<fator>
<fator_>

<lista_instrucoes_>
<comando_atribuicao_>
<comando_manipulacao_lista_>
<comando_intermediario_atr_mani>
<comando_intermediario_atr_mani_>
<lista_id_>
<lista_entrada_>
<lista_expressoes_>
<lista_comandos_>
#Grammar
<programa> ::= #100 pr_begin <lista_instrucoes> pr_end #101 ;
<lista_instrucoes> ::= <instrucao> ";" <lista_instrucoes_> ;
<lista_instrucoes_> ::= î | <lista_instrucoes> ;
<instrucao> ::= <dec_variavel> | <comando> ;
<comando> ::= <comando_intermediario_atr_mani> | <comando_entrada_dados> | <comando_saida_dados> | <comando_selecao> | <comando_repeticao> ;


<dec_variavel> ::= <tipo> <lista_id> #119 ;
<lista_id>::= identificador #121 <lista_id_> ;
<lista_id_>::= î | "," <lista_id> ;
<tipo_simples> ::= pr_bool #120 | pr_int #120 | pr_float #120 | pr_string #120 ;
<tipo> ::= <tipo_simples> | <lista_tipo> ;
<lista_tipo> ::= pr_list "(" <tipo_simples> "," cint ")" ;

<comando_atribuicao_> ::= "=" <expressao> #122 | "<-" <expressao> #122 ;

<comando_manipulacao_lista_> ::= pr_add "(" <elemento> "," <posicao> ")" | pr_delete "(" <posicao> ")" ;

<comando_intermediario_atr_mani> ::= identificador #121 <comando_intermediario_atr_mani_> ;
<comando_intermediario_atr_mani_> ::= <comando_manipulacao_lista_> | <comando_atribuicao_> ;

<elemento> ::= <expressao> ;
<posicao> ::= <expressao> ;

<comando_entrada_dados> ::= pr_read "(" <lista_entrada> ")" ;
<lista_entrada> ::= <string_read> identificador #123 <lista_entrada_> ;
<lista_entrada_> ::= î | "," <lista_entrada> ;
<string_read> ::= cstring #124 "," | î ;

<comando_saida_dados> ::= pr_print "(" <lista_expressoes> ")" #118 ;
<lista_expressoes> ::= <expressao> #102 <lista_expressoes_> ;
<lista_expressoes_> ::= î | "," <lista_expressoes> ;


<comando_selecao> ::= pr_if <expressao> #125 <lista_comandos> <else> pr_end #126 ;
<lista_comandos> ::= <comando> ";" <lista_comandos_> ;
<lista_comandos_> ::= î | <lista_comandos> ;
<else> ::= pr_else #127 <lista_comandos> | î ;

<comando_repeticao> ::= pr_do #128 <lista_comandos> pr_until <expressao> #129 ;




<expressao> ::= <valor> <expressao_> ; 
<expressao_> ::= î | pr_and <valor> #113 <expressao_> | pr_or <valor> #114 <expressao_> ; 
<valor> ::= <relacional> | pr_true #115 | pr_false #116 | pr_not <valor> #117 ; 
<relacional> ::= <aritmetica> <relacional_> ; 
<relacional_> ::= î | <operador_relacional> #111 <aritmetica> #112 ; 
<operador_relacional>::= "==" | "~=" | "<" | ">" ; 
<aritmetica> ::= <termo> <aritmetica_> ; 
<aritmetica_> ::= î | "+" <termo> #106 <aritmetica_> | "-" <termo> #107 <aritmetica_> ; 
<termo> ::= <fator> <termo_> ; 
<termo_> ::= î | "*" <fator> #108 <termo_> | "/" <fator> #109 <termo_> ; 
<fator> ::= identificador #130 <fator_> | 
 cint #103 | 
 cfloat #104 | 
 cstring #105 | 
 "(" <expressao> ")" | 
 "+" <fator> | 
 "-" <fator> #110 ; 
<fator_> ::= î | pr_count | pr_size | pr_elementof "(" <expressao> ")" ;

